{Input Port des zweiten GALS-Wrappers}

{01: konkret; alle Gatter einzeln}
{02: Komponenten zusammengefasst:}
{      AND-DFF (and-d0_ff_set_02.txt)}
{      AND3-AND-OR (and3-and-or_02.txt)}
{      AND-AND-OR (and-and-or-abbc_02a.txt)}
{03: stark abstrahiert}
{04: korrigiert. In Zustand 3 können vorm REQ_A1+ mehrere ACK_INT+/- auftreten,}
{    die alle keinen Effekt haben wegen ST=1, ACKEN=0. Deshalb kann der IP in}
{    Zustand 3 jetzt beliebig viele ACK_INT+/- verschlucken. Dabei geht er erst}
{    in den nächsten Zustand, wenn jedes ACK_INT+/- -Paar vollständig}
{    verschluckt (mit AC_INT- abgeschlossen) wurde}


PLACE
    ip.p.LCLKM,  {Input/Outputflanken}
    ip.p.n_LCLKM,
    {ip.p.INT_CLK entfällt}
    {ip.p.ACK_INT => ip.ic.p.ACKC}
    ip.p.DATAV_IN,
    ip.p.n_DATAV_IN,
    ip.p.pegel_LCLKM_0,  {Pegelplätze}
    ip.p.pegel_LCLKM_1,
    ip.p.pegel_DATAV_IN_0,
    ip.p.pegel_DATAV_IN_1,
    ip.p.ACK_A+,  {Hilfsplätze}
    ip.p.n_ACK_A+,
    ip.p.pegel_ACK_EN_0,
    ip.p.ACK_EN+,
    ip.p.n_ACK_EN+,
    ip.p.ACKC+,
    ip.p.n_ACKC+,
    ip.p.ACKC-_abzuraeumen, {wenn markiert, ACKC(-) abzuräumen, bevor IP weiterschalten kann}
    ip.p.n_ACKC-_abzuraeumen, {Komplementärplatz}

    {Input Controller im IP}
    ip.ic.p.0,  {Zustände des Input Controllers}
    ip.ic.p.1,
    ip.ic.p.2,
    ip.ic.p.3,
    ip.ic.p.4,
    ip.ic.p.5,
    ip.ic.p.6,
    ip.ic.p.7,
    ip.ic.p.8,
    ip.ic.p.REQ_A1,    {Eingabesignale als Flanken}
    ip.ic.p.n_REQ_A1,
    ip.ic.p.ST,
    ip.ic.p.n_ST,
    ip.ic.p.STOP,
    ip.ic.p.n_STOP,
    ip.ic.p.ACKC,
    ip.ic.p.n_ACKC,
    ip.ic.p.ACKI1,
    ip.ic.p.n_ACKI1,
    ip.ic.p.ACK_A,  {Ausgabesignale als Flanken}
    ip.ic.p.n_ACK_A,
    ip.ic.p.RST,
    ip.ic.p.n_RST,
    ip.ic.p.REQ_INT,
    ip.ic.p.n_REQ_INT,
    ip.ic.p.REQI1,
    ip.ic.p.n_REQI1;

MARKING
    ip.p.n_LCLKM: 1,
    ip.p.n_DATAV_IN: 1,
    ip.p.pegel_LCLKM_0: 1,
    ip.p.pegel_DATAV_IN_0: 1,
    ip.p.pegel_ACK_EN_0: 1,
    ip.p.n_ACK_A+: 1,
    ip.p.n_ACK_EN+: 1,
    ip.p.n_ACKC+: 1,
    ip.p.n_ACKC-_abzuraeumen: 1,

    ip.ic.p.0: 1,
    ip.ic.p.n_REQ_A1: 1,
    ip.ic.p.n_ST: 1,
    ip.ic.p.n_STOP: 1,
    ip.ic.p.n_ACKC: 1,
    ip.ic.p.n_ACKI1: 1,
    ip.ic.p.n_ACK_A: 1,
    ip.ic.p.n_RST: 1,
    ip.ic.p.n_REQ_INT: 1,
    ip.ic.p.n_REQI1: 1;


{Pegelkodierung: LCLKM, DATAV_IN}

TRANSITION ip.t.LCLKM_0_pegel_1_x
CONSUME ip.p.LCLKM: 1, ip.p.pegel_LCLKM_1: 1;
PRODUCE ip.p.n_LCLKM: 1, ip.p.pegel_LCLKM_0: 1;

TRANSITION ip.t.LCLKM_1_pegel_0_0
CONSUME ip.p.LCLKM: 1, ip.p.pegel_LCLKM_0: 1, ip.p.pegel_DATAV_IN_0: 1;
PRODUCE ip.p.n_LCLKM: 1, ip.p.pegel_LCLKM_1: 1, ip.p.pegel_DATAV_IN_0: 1;

TRANSITION ip.t.LCLKM_1_pegel_0_1
CONSUME ip.p.LCLKM: 1, ip.p.pegel_LCLKM_0: 1, ip.p.pegel_DATAV_IN_1: 1,
        ip.p.n_DATAV_IN: 1;
PRODUCE ip.p.n_LCLKM: 1, ip.p.pegel_LCLKM_1: 1, ip.p.pegel_DATAV_IN_0: 1,
        ip.p.DATAV_IN: 1;


{Input Controller im IP}

TRANSITION ip.ic.t.01
CONSUME ip.ic.p.0: 1, ip.ic.p.REQ_A1: 1, ip.ic.p.n_REQ_INT: 1, ip.ic.p.n_RST: 1,
        ip.ic.p.n_ACK_A: 1, ip.p.n_ACK_A+: 1;
PRODUCE ip.ic.p.1: 1, ip.ic.p.n_REQ_A1: 1, ip.ic.p.REQ_INT: 1, ip.ic.p.RST: 1,
        ip.ic.p.ACK_A: 1, ip.p.ACK_A+: 1;

TRANSITION ip.ic.t.12
CONSUME ip.ic.p.1: 1, ip.ic.p.ACKC: 1, ip.p.ACKC+: 1, ip.ic.p.REQ_A1: 1,
        ip.ic.p.n_REQ_INT: 1, ip.ic.p.n_RST: 1, ip.ic.p.n_ACK_A: 1;
PRODUCE ip.ic.p.2: 1, ip.ic.p.n_ACKC: 1, ip.p.n_ACKC+: 1, ip.ic.p.n_REQ_A1: 1,
        ip.ic.p.REQ_INT: 1, ip.ic.p.RST: 1, ip.ic.p.ACK_A: 1;

TRANSITION ip.ic.t.21
CONSUME ip.ic.p.2: 1, ip.ic.p.ACKC: 1, ip.ic.p.REQ_A1: 1, ip.ic.p.n_REQ_INT: 1,
        ip.ic.p.n_RST: 1, ip.ic.p.n_ACK_A: 1, ip.p.n_ACK_A+: 1;
PRODUCE ip.ic.p.1: 1, ip.ic.p.n_ACKC: 1, ip.ic.p.n_REQ_A1: 1,
        ip.ic.p.REQ_INT: 1, ip.ic.p.RST: 1, ip.ic.p.ACK_A: 1, ip.p.ACK_A+: 1;

TRANSITION ip.ic.t.23
CONSUME ip.ic.p.2: 1, ip.ic.p.ACKC: 1, ip.ic.p.ST: 1;
PRODUCE ip.ic.p.3: 1, ip.ic.p.n_ACKC: 1, ip.ic.p.n_ST: 1;

TRANSITION ip.ic.t.34
CONSUME ip.ic.p.3: 1, ip.ic.p.STOP: 1, ip.ic.p.n_RST: 1,
        ip.p.n_ACKC-_abzuraeumen: 1;
PRODUCE ip.ic.p.4: 1, ip.ic.p.n_STOP: 1, ip.ic.p.RST: 1,
        ip.p.n_ACKC-_abzuraeumen: 1;

TRANSITION ip.ic.t.40
CONSUME ip.ic.p.4: 1, ip.ic.p.STOP: 1, ip.ic.p.ST: 1, ip.ic.p.n_RST: 1;
PRODUCE ip.ic.p.0: 1, ip.ic.p.n_STOP: 1, ip.ic.p.n_ST: 1, ip.ic.p.RST: 1;

TRANSITION ip.ic.t.35
CONSUME ip.ic.p.3: 1, ip.ic.p.REQ_A1: 1, ip.ic.p.n_REQI1: 1,
        ip.p.n_ACKC-_abzuraeumen: 1;
PRODUCE ip.ic.p.5: 1, ip.ic.p.n_REQ_A1: 1, ip.ic.p.REQI1: 1,
        ip.p.n_ACKC-_abzuraeumen: 1;

TRANSITION ip.ic.t.56
CONSUME ip.ic.p.5: 1, ip.ic.p.ACKI1: 1, ip.p.n_ACK_EN+: 1, ip.ic.p.n_REQI1: 1,
        ip.p.pegel_ACK_EN_0: 1;
PRODUCE ip.ic.p.6: 1, ip.ic.p.n_ACKI1: 1, ip.p.ACK_EN+: 1, ip.ic.p.REQI1: 1;

TRANSITION ip.ic.t.67
CONSUME ip.ic.p.6: 1, ip.ic.p.ACKI1: 1, ip.ic.p.ACKC: 1;
PRODUCE ip.ic.p.7: 1, ip.ic.p.n_ACKI1: 1, ip.ic.p.n_ACKC: 1;

TRANSITION ip.ic.t.78
CONSUME ip.ic.p.7: 1, ip.ic.p.ACKC: 1, ip.ic.p.n_ACK_A: 1, ip.ic.p.n_RST: 1;
PRODUCE ip.ic.p.8: 1, ip.ic.p.n_ACKC: 1, ip.ic.p.ACK_A: 1, ip.ic.p.RST: 1;

TRANSITION ip.ic.t.80
CONSUME ip.ic.p.8: 1, ip.ic.p.REQ_A1: 1, ip.ic.p.ST: 1, ip.ic.p.n_ACK_A: 1,
        ip.ic.p.n_RST: 1;
PRODUCE ip.ic.p.0: 1, ip.ic.p.n_REQ_A1: 1, ip.ic.p.n_ST: 1, ip.ic.p.ACK_A: 1,
        ip.ic.p.RST: 1, ip.p.pegel_ACK_EN_0: 1;



TRANSITION ip.t.DATAV_IN+_durch_ACK_A
CONSUME ip.p.ACK_A+: 1, ip.p.pegel_ACK_EN_0: 1, ip.p.pegel_DATAV_IN_0: 1,
        ip.p.n_DATAV_IN: 1;
PRODUCE ip.p.n_ACK_A+: 1, ip.p.pegel_ACK_EN_0: 1, ip.p.pegel_DATAV_IN_1: 1,
        ip.p.DATAV_IN: 1;

TRANSITION ip.t.DATAV_IN+_durch_ACK_C
CONSUME ip.p.ACKC+: 1, ip.p.ACK_EN+: 1, ip.p.pegel_DATAV_IN_0: 1,
        ip.p.n_DATAV_IN: 1;
PRODUCE ip.p.n_ACKC+: 1, ip.p.n_ACK_EN+: 1, ip.p.pegel_DATAV_IN_1: 1,
        ip.p.DATAV_IN: 1;



{Wenn IC im Zustand 3, dann können beliebig viele Flankenpaare ACKC+/ACKC-}
{abgeräumt werden}
TRANSITION ip.t.ACKC+_abraeumen
CONSUME ip.ic.p.3: 1, ip.p.n_ACKC-_abzuraeumen: 1, ip.ic.p.ACKC: 1,
        ip.p.ACKC+: 1;
PRODUCE ip.ic.p.3: 1, ip.p.ACKC-_abzuraeumen: 1;

TRANSITION ip.t.ACKC-_abraeumen
CONSUME ip.p.ACKC-_abzuraeumen: 1, ip.ic.p.ACKC: 1;
PRODUCE ip.p.n_ACKC-_abzuraeumen: 1;


{Anbindung an Außenwelt}

{LCLKM produzieren}
TRANSITION ip.t.LCLKM
CONSUME ip.p.n_LCLKM: 1;
PRODUCE ip.p.LCLKM: 1;

{REQ_A1 produzieren}
TRANSITION ip.t.REQ_A1
CONSUME ip.ic.p.n_REQ_A1: 1;
PRODUCE ip.ic.p.REQ_A1: 1;

{STOP produzieren}
TRANSITION ip.t.STOP
CONSUME ip.ic.p.n_STOP: 1;
PRODUCE ip.ic.p.STOP: 1;

{ACKI1 produzieren}
TRANSITION ip.t.ACKI1
CONSUME ip.ic.p.n_ACKI1: 1;
PRODUCE ip.ic.p.ACKI1: 1;

{ST produzieren}
TRANSITION ip.t.ST
CONSUME ip.ic.p.n_ST: 1;
PRODUCE ip.ic.p.ST: 1;

{ACK_INT/ACKC produzieren}
TRANSITION ip.t.ACK_INT
CONSUME ip.ic.p.n_ACKC: 1;
PRODUCE ip.ic.p.ACKC: 1;

{ACKC+ produzieren}
TRANSITION ip.t.ACKC+
CONSUME ip.p.n_ACKC+: 1;
PRODUCE ip.p.ACKC+: 1;

{ACK_A abräumen}
TRANSITION ip.t.ACK_A
CONSUME ip.ic.p.ACK_A: 1;
PRODUCE ip.ic.p.n_ACK_A: 1;

{REQI1 abräumen}
TRANSITION ip.t.REQI1
CONSUME ip.ic.p.REQI1: 1;
PRODUCE ip.ic.p.n_REQI1: 1;

{RST abräumen}
TRANSITION ip.t.RST
CONSUME ip.ic.p.RST: 1;
PRODUCE ip.ic.p.n_RST: 1;

{REQ_INT abräumen}
TRANSITION ip.t.REQ_INT
CONSUME ip.ic.p.REQ_INT: 1;
PRODUCE ip.ic.p.n_REQ_INT: 1;

{DATAV_IN abräumen}
TRANSITION ip.t.DATAV_IN
CONSUME ip.p.DATAV_IN: 1;
PRODUCE ip.p.n_DATAV_IN: 1;



{ vim: set ft=lola: }
