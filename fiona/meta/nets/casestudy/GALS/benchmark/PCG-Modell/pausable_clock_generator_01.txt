{Pausable Clock Generator des zweiten Wrappers (unver‰ndert zu dem im ersten)}

PLACE
    {NOR im PCG}
    pcg.nor.p.LCLK,  {a}  {Input-Flanken}
    pcg.nor.p.n_LCLK,  {n_a}
    pcg.nor.p.STOPI,  {b}
    pcg.nor.p.n_STOPI,  {n_b}
    {pcg.nor.p.out => pcg.mce.p.RCLKD, pcg.mutex1.p.RCLK, pcg.mutex2.p.RCLK}
    pcg.nor.p.pegel_0_0,  {alte Pegel}
    pcg.nor.p.pegel_0_1,
    pcg.nor.p.pegel_1_0,
    pcg.nor.p.pegel_1_1,

    {1. Mutex im PCG}
    pcg.mutex1.p.REQI1,  {a_in}  {Input: Flanke}
    pcg.mutex1.p.n_REQI1,  {n_a_in}
    pcg.mutex1.p.RCLK,  {b_in}
    pcg.mutex1.p.n_RCLK,  {n_b_in}
    pcg.mutex1.p.ACKI1,  {a_out}  {Output: Flanke}
    pcg.mutex1.p.n_ACKI1,  {n_a_out}
    {pcg.mutex1.p.b_out => pcg.and.p.b}
    pcg.mutex1.p.pegel_0_0_0_0,  {zu speichernde Pegel}
    pcg.mutex1.p.pegel_0_1_0_1,
    pcg.mutex1.p.pegel_1_0_1_0,  {Pegel: (a_alt, b_alt, a_out, b_out)}
    pcg.mutex1.p.pegel_1_1_0_1,
    pcg.mutex1.p.pegel_1_1_1_0,

    {2. Mutex im PCG}
    pcg.mutex2.p.STRETCH,  {a_in}  {Input: Flanke}
    pcg.mutex2.p.n_STRETCH,  {n_a_in}
    pcg.mutex2.p.RCLK,  {b_in}
    pcg.mutex2.p.n_RCLK,  {n_b_in}
    {pcg.mutex2.p.a_out entfaellt}  {Output: Flanke}
    {pcg.mutex2.p.b_out => pcg.and.p.a}
    pcg.mutex2.p.pegel_0_0_0_0,  {zu speichernde Pegel}
    pcg.mutex2.p.pegel_0_1_0_1,
    pcg.mutex2.p.pegel_1_0_1_0,  {Pegel: (a_alt, b_alt, a_out, b_out)}
    pcg.mutex2.p.pegel_1_1_0_1,
    pcg.mutex2.p.pegel_1_1_1_0,

    {AND im PCG}
    pcg.and.p.a,  {Input-Flanken}
    pcg.and.p.n_a,
    pcg.and.p.b,
    pcg.and.p.n_b,
    {pcg.and.p.out => pcg.mce.p.CLK_GRANT}  {Output-Flanke}
    pcg.and.p.pegel_0_0,  {alte Pegel}
    pcg.and.p.pegel_0_1,
    pcg.and.p.pegel_1_0,
    pcg.and.p.pegel_1_1,

    {MCE im PCG}
    pcg.mce.p.CLK_GRANT,  {a}
    pcg.mce.p.n_CLK_GRANT,  {n_a}
    pcg.mce.p.RCLKD,  {b}
    pcg.mce.p.n_RCLKD,  {n_b}
    pcg.mce.p.LCLK, {out}  {Verzweigung: pcg.nor.p.LCLK}  {Output-Flanke}
    pcg.mce.p.n_LCLK, {n_out}  {Verzweigung: pcg.nor.p.n_LCLK}
    pcg.mce.p.pegel_0_0_0,
    pcg.mce.p.pegel_0_1_0,  {alte Pegel (a_b_c)}
    pcg.mce.p.pegel_1_0_0,  {p.pegel_0_0_1, p.pegel_1_1_0 existieren nicht}
    pcg.mce.p.pegel_0_1_1,
    pcg.mce.p.pegel_1_0_1,
    pcg.mce.p.pegel_1_1_1;


MARKING
    pcg.nor.p.n_LCLK: 1,
    pcg.nor.p.n_STOPI: 1,
    pcg.nor.p.pegel_0_1: 1,  {STOPI initial 1}

    pcg.mutex1.p.n_REQI1: 1,
    pcg.mutex1.p.n_RCLK: 1,
    pcg.mutex1.p.n_ACKI1: 1,
    pcg.mutex1.p.pegel_0_0_0_0: 1,

    pcg.mutex2.p.n_STRETCH: 1,
    pcg.mutex2.p.n_RCLK: 1,
    pcg.mutex2.p.pegel_0_0_0_0: 1,

    pcg.and.p.n_a: 1,
    pcg.and.p.n_b: 1,
    pcg.and.p.pegel_0_0: 1,

    pcg.mce.p.n_CLK_GRANT: 1,
    pcg.mce.p.n_RCLKD: 1,
    pcg.mce.p.n_LCLK: 1,
    pcg.mce.p.pegel_0_0_0: 1;



{NOR im PCG}

TRANSITION pcg.nor.t.a1_pegel_0_0
CONSUME pcg.nor.p.LCLK: 1, pcg.nor.p.pegel_0_0: 1, pcg.mce.p.n_RCLKD: 1,
        pcg.mutex1.p.n_RCLK: 1, pcg.mutex2.p.n_RCLK: 1;
PRODUCE pcg.nor.p.pegel_1_0: 1, pcg.mce.p.RCLKD: 1, pcg.mutex1.p.RCLK: 1,
        pcg.mutex2.p.RCLK: 1, pcg.nor.p.n_LCLK: 1;

TRANSITION pcg.nor.t.a1_pegel_0_1
CONSUME pcg.nor.p.LCLK: 1, pcg.nor.p.pegel_0_1: 1;
PRODUCE pcg.nor.p.pegel_1_1: 1, pcg.nor.p.n_LCLK: 1;

TRANSITION pcg.nor.t.a0_pegel_1_0
CONSUME pcg.nor.p.LCLK: 1, pcg.nor.p.pegel_1_0: 1, pcg.mce.p.n_RCLKD: 1,
        pcg.mutex1.p.n_RCLK: 1, pcg.mutex2.p.n_RCLK: 1;
PRODUCE pcg.nor.p.pegel_0_0: 1, pcg.mce.p.RCLKD: 1, pcg.mutex1.p.RCLK: 1,
        pcg.mutex2.p.RCLK: 1, pcg.nor.p.n_LCLK: 1;

TRANSITION pcg.nor.t.a0_pegel_1_1
CONSUME pcg.nor.p.LCLK: 1, pcg.nor.p.pegel_1_1: 1;
PRODUCE pcg.nor.p.pegel_0_1: 1, pcg.nor.p.n_LCLK: 1;


TRANSITION pcg.nor.t.b1_pegel_0_0
CONSUME pcg.nor.p.STOPI: 1, pcg.nor.p.pegel_0_0: 1, pcg.mce.p.n_RCLKD: 1,
        pcg.mutex1.p.n_RCLK: 1, pcg.mutex2.p.n_RCLK: 1;
PRODUCE pcg.nor.p.pegel_0_1: 1, pcg.mce.p.RCLKD: 1, pcg.mutex1.p.RCLK: 1,
        pcg.mutex2.p.RCLK: 1, pcg.nor.p.n_STOPI: 1;

TRANSITION pcg.nor.t.b0_pegel_0_1
CONSUME pcg.nor.p.STOPI: 1, pcg.nor.p.pegel_0_1: 1, pcg.mce.p.n_RCLKD: 1,
        pcg.mutex1.p.n_RCLK: 1, pcg.mutex2.p.n_RCLK: 1;
PRODUCE pcg.nor.p.pegel_0_0: 1, pcg.mce.p.RCLKD: 1, pcg.mutex1.p.RCLK: 1,
        pcg.mutex2.p.RCLK: 1, pcg.nor.p.n_STOPI: 1;

TRANSITION pcg.nor.t.b1_pegel_1_0
CONSUME pcg.nor.p.STOPI: 1, pcg.nor.p.pegel_1_0: 1;
PRODUCE pcg.nor.p.pegel_1_1: 1, pcg.nor.p.n_STOPI: 1;

TRANSITION pcg.nor.t.b0_pegel_1_1
CONSUME pcg.nor.p.STOPI: 1, pcg.nor.p.pegel_1_1: 1;
PRODUCE pcg.nor.p.pegel_1_0: 1, pcg.nor.p.n_STOPI: 1;



{1. Mutex im PCG}

TRANSITION pcg.mutex1.t.a1_pegel_0_0_0_0
CONSUME pcg.mutex1.p.REQI1: 1, pcg.mutex1.p.pegel_0_0_0_0: 1,
        pcg.mutex1.p.n_ACKI1: 1;
PRODUCE pcg.mutex1.p.pegel_1_0_1_0: 1, pcg.mutex1.p.ACKI1: 1,
        pcg.mutex1.p.n_REQI1: 1;

TRANSITION pcg.mutex1.t.a1_pegel_0_1_0_1
CONSUME pcg.mutex1.p.REQI1: 1, pcg.mutex1.p.pegel_0_1_0_1: 1;
PRODUCE pcg.mutex1.p.pegel_1_1_0_1: 1, pcg.mutex1.p.n_REQI1: 1;

TRANSITION pcg.mutex1.t.a0_pegel_1_0_1_0
CONSUME pcg.mutex1.p.REQI1: 1, pcg.mutex1.p.pegel_1_0_1_0: 1,
        pcg.mutex1.p.n_ACKI1: 1;
PRODUCE pcg.mutex1.p.pegel_0_0_0_0: 1, pcg.mutex1.p.ACKI1: 1,
        pcg.mutex1.p.n_REQI1: 1;

TRANSITION pcg.mutex1.t.a0_pegel_1_1_0_1
CONSUME pcg.mutex1.p.REQI1: 1, pcg.mutex1.p.pegel_1_1_0_1: 1;
PRODUCE pcg.mutex1.p.pegel_0_1_0_1: 1, pcg.mutex1.p.n_REQI1: 1;

TRANSITION pcg.mutex1.t.a0_pegel_1_1_1_0
CONSUME pcg.mutex1.p.REQI1: 1, pcg.mutex1.p.pegel_1_1_1_0: 1,
        pcg.mutex1.p.n_ACKI1: 1, pcg.and.p.n_b: 1;
PRODUCE pcg.mutex1.p.pegel_0_1_0_1: 1, pcg.mutex1.p.ACKI1: 1, pcg.and.p.b: 1,
        pcg.mutex1.p.n_REQI1: 1;


TRANSITION pcg.mutex1.t.b1_pegel_0_0_0_0
CONSUME pcg.mutex1.p.RCLK: 1, pcg.mutex1.p.pegel_0_0_0_0: 1, pcg.and.p.n_b: 1;
PRODUCE pcg.mutex1.p.pegel_0_1_0_1: 1, pcg.and.p.b: 1, pcg.mutex1.p.n_RCLK: 1;

TRANSITION pcg.mutex1.t.b0_pegel_0_1_0_1
CONSUME pcg.mutex1.p.RCLK: 1, pcg.mutex1.p.pegel_0_1_0_1: 1, pcg.and.p.n_b: 1;
PRODUCE pcg.mutex1.p.pegel_0_0_0_0: 1, pcg.and.p.b: 1, pcg.mutex1.p.n_RCLK: 1;

TRANSITION pcg.mutex1.t.b1_pegel_1_0_1_0
CONSUME pcg.mutex1.p.RCLK: 1, pcg.mutex1.p.pegel_1_0_1_0: 1;
PRODUCE pcg.mutex1.p.pegel_1_1_1_0: 1, pcg.mutex1.p.n_RCLK: 1;

TRANSITION pcg.mutex1.t.b0_pegel_1_1_0_1
CONSUME pcg.mutex1.p.RCLK: 1, pcg.mutex1.p.pegel_1_1_0_1: 1,
        pcg.mutex1.p.n_ACKI1: 1, pcg.and.p.n_b: 1;
PRODUCE pcg.mutex1.p.pegel_1_0_1_0: 1, pcg.mutex1.p.ACKI1: 1, pcg.and.p.b: 1,
        pcg.mutex1.p.n_RCLK: 1;

TRANSITION pcg.mutex1.t.b0_pegel_1_1_1_0
CONSUME pcg.mutex1.p.RCLK: 1, pcg.mutex1.p.pegel_1_1_1_0: 1;
PRODUCE pcg.mutex1.p.pegel_1_0_1_0: 1, pcg.mutex1.p.n_RCLK: 1;



{2. Mutex im PCG}

TRANSITION pcg.mutex2.t.a1_pegel_0_0_0_0
CONSUME pcg.mutex2.p.STRETCH: 1, pcg.mutex2.p.pegel_0_0_0_0: 1;
PRODUCE pcg.mutex2.p.pegel_1_0_1_0: 1, pcg.mutex2.p.n_STRETCH: 1;

TRANSITION pcg.mutex2.t.a1_pegel_0_1_0_1
CONSUME pcg.mutex2.p.STRETCH: 1, pcg.mutex2.p.pegel_0_1_0_1: 1;
PRODUCE pcg.mutex2.p.pegel_1_1_0_1: 1, pcg.mutex2.p.n_STRETCH: 1;

TRANSITION pcg.mutex2.t.a0_pegel_1_0_1_0
CONSUME pcg.mutex2.p.STRETCH: 1, pcg.mutex2.p.pegel_1_0_1_0: 1;
PRODUCE pcg.mutex2.p.pegel_0_0_0_0: 1, pcg.mutex2.p.n_STRETCH: 1;

TRANSITION pcg.mutex2.t.a0_pegel_1_1_0_1
CONSUME pcg.mutex2.p.STRETCH: 1, pcg.mutex2.p.pegel_1_1_0_1: 1;
PRODUCE pcg.mutex2.p.pegel_0_1_0_1: 1, pcg.mutex2.p.n_STRETCH: 1;

TRANSITION pcg.mutex2.t.a0_pegel_1_1_1_0
CONSUME pcg.mutex2.p.STRETCH: 1, pcg.mutex2.p.pegel_1_1_1_0: 1,
        pcg.and.p.n_a: 1;
PRODUCE pcg.mutex2.p.pegel_0_1_0_1: 1, pcg.and.p.a: 1,
        pcg.mutex2.p.n_STRETCH: 1;


TRANSITION pcg.mutex2.t.b1_pegel_0_0_0_0
CONSUME pcg.mutex2.p.RCLK: 1, pcg.mutex2.p.pegel_0_0_0_0: 1, pcg.and.p.n_a: 1;
PRODUCE pcg.mutex2.p.pegel_0_1_0_1: 1, pcg.and.p.a: 1, pcg.mutex2.p.n_RCLK: 1;

TRANSITION pcg.mutex2.t.b0_pegel_0_1_0_1
CONSUME pcg.mutex2.p.RCLK: 1, pcg.mutex2.p.pegel_0_1_0_1: 1, pcg.and.p.n_a: 1;
PRODUCE pcg.mutex2.p.pegel_0_0_0_0: 1, pcg.and.p.a: 1, pcg.mutex2.p.n_RCLK: 1;

TRANSITION pcg.mutex2.t.b1_pegel_1_0_1_0
CONSUME pcg.mutex2.p.RCLK: 1, pcg.mutex2.p.pegel_1_0_1_0: 1;
PRODUCE pcg.mutex2.p.pegel_1_1_1_0: 1, pcg.mutex2.p.n_RCLK: 1;

TRANSITION pcg.mutex2.t.b0_pegel_1_1_0_1
CONSUME pcg.mutex2.p.RCLK: 1, pcg.mutex2.p.pegel_1_1_0_1: 1, pcg.and.p.n_a: 1;
PRODUCE pcg.mutex2.p.pegel_1_0_1_0: 1, pcg.and.p.a: 1, pcg.mutex2.p.n_RCLK: 1;

TRANSITION pcg.mutex2.t.b0_pegel_1_1_1_0
CONSUME pcg.mutex2.p.RCLK: 1, pcg.mutex2.p.pegel_1_1_1_0: 1;
PRODUCE pcg.mutex2.p.pegel_1_0_1_0: 1, pcg.mutex2.p.n_RCLK: 1;



{AND im PCG}

TRANSITION pcg.and.t.a1_pegel_0_0
CONSUME pcg.and.p.a: 1, pcg.and.p.pegel_0_0: 1;
PRODUCE pcg.and.p.pegel_1_0: 1, pcg.and.p.n_a: 1;

TRANSITION pcg.and.t.a1_pegel_0_1
CONSUME pcg.and.p.a: 1, pcg.and.p.pegel_0_1: 1, pcg.mce.p.n_CLK_GRANT: 1;
PRODUCE pcg.and.p.pegel_1_1: 1, pcg.mce.p.CLK_GRANT: 1, pcg.and.p.n_a: 1;

TRANSITION pcg.and.t.a0_pegel_1_0
CONSUME pcg.and.p.a: 1, pcg.and.p.pegel_1_0: 1;
PRODUCE pcg.and.p.pegel_0_0: 1, pcg.and.p.n_a: 1;

TRANSITION pcg.and.t.a0_pegel_1_1
CONSUME pcg.and.p.a: 1, pcg.and.p.pegel_1_1: 1, pcg.mce.p.n_CLK_GRANT: 1;
PRODUCE pcg.and.p.pegel_0_1: 1, pcg.mce.p.CLK_GRANT: 1, pcg.and.p.n_a: 1;


TRANSITION pcg.and.t.b1_pegel_0_0
CONSUME pcg.and.p.b: 1, pcg.and.p.pegel_0_0: 1;
PRODUCE pcg.and.p.pegel_0_1: 1, pcg.and.p.n_b: 1;

TRANSITION pcg.and.t.b0_pegel_0_1
CONSUME pcg.and.p.b: 1, pcg.and.p.pegel_0_1: 1;
PRODUCE pcg.and.p.pegel_0_0: 1, pcg.and.p.n_b: 1;

TRANSITION pcg.and.t.b1_pegel_1_0
CONSUME pcg.and.p.b: 1, pcg.and.p.pegel_1_0: 1, pcg.mce.p.n_CLK_GRANT: 1;
PRODUCE pcg.and.p.pegel_1_1: 1, pcg.mce.p.CLK_GRANT: 1, pcg.and.p.n_b: 1;

TRANSITION pcg.and.t.b0_pegel_1_1
CONSUME pcg.and.p.b: 1, pcg.and.p.pegel_1_1: 1, pcg.mce.p.n_CLK_GRANT: 1;
PRODUCE pcg.and.p.pegel_1_0: 1, pcg.mce.p.CLK_GRANT: 1, pcg.and.p.n_b: 1;



{MCE im PCG}

TRANSITION pcg.mce.t.a1_pegel_0_0_0
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_0_0_0: 1;
PRODUCE pcg.mce.p.pegel_1_0_0: 1, pcg.mce.p.n_CLK_GRANT: 1;

TRANSITION pcg.mce.t.a1_pegel_0_1_0
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_0_1_0: 1, pcg.mce.p.n_LCLK: 1,
        pcg.nor.p.n_LCLK: 1;
PRODUCE pcg.mce.p.pegel_1_1_1: 1, pcg.mce.p.LCLK: 1, pcg.nor.p.LCLK: 1,
        pcg.mce.p.n_CLK_GRANT: 1;

TRANSITION pcg.mce.t.a0_pegel_1_0_0
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_1_0_0: 1;
PRODUCE pcg.mce.p.pegel_0_0_0: 1, pcg.mce.p.n_CLK_GRANT: 1;

TRANSITION pcg.mce.t.a0_pegel_1_0_1
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_1_0_1: 1, pcg.mce.p.n_LCLK: 1,
        pcg.nor.p.n_LCLK: 1;
PRODUCE pcg.mce.p.pegel_0_0_0: 1, pcg.mce.p.LCLK: 1, pcg.nor.p.LCLK: 1,
        pcg.mce.p.n_CLK_GRANT: 1;

TRANSITION pcg.mce.t.a1_pegel_0_1_1
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_0_1_1: 1;
PRODUCE pcg.mce.p.pegel_1_1_1: 1, pcg.mce.p.n_CLK_GRANT: 1;

TRANSITION pcg.mce.t.a0_pegel_1_1_1
CONSUME pcg.mce.p.CLK_GRANT: 1, pcg.mce.p.pegel_1_1_1: 1;
PRODUCE pcg.mce.p.pegel_0_1_1: 1, pcg.mce.p.n_CLK_GRANT: 1;


TRANSITION pcg.mce.t.b1_pegel_0_0_0
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_0_0_0: 1;
PRODUCE pcg.mce.p.pegel_0_1_0: 1, pcg.mce.p.n_RCLKD: 1;

TRANSITION pcg.mce.t.b0_pegel_0_1_0
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_0_1_0: 1;
PRODUCE pcg.mce.p.pegel_0_0_0: 1, pcg.mce.p.n_RCLKD: 1;

TRANSITION pcg.mce.t.b1_pegel_1_0_0
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_1_0_0: 1, pcg.mce.p.n_LCLK: 1,
        pcg.nor.p.n_LCLK: 1;
PRODUCE pcg.mce.p.pegel_1_1_1: 1, pcg.mce.p.LCLK: 1, pcg.nor.p.LCLK: 1,
        pcg.mce.p.n_RCLKD: 1;

TRANSITION pcg.mce.t.b1_pegel_1_0_1
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_1_0_1: 1;
PRODUCE pcg.mce.p.pegel_1_1_1: 1, pcg.mce.p.n_RCLKD: 1;

TRANSITION pcg.mce.t.b0_pegel_0_1_1
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_0_1_1: 1, pcg.mce.p.n_LCLK: 1,
        pcg.nor.p.n_LCLK: 1;
PRODUCE pcg.mce.p.pegel_0_0_0: 1, pcg.mce.p.LCLK: 1, pcg.nor.p.LCLK: 1,
        pcg.mce.p.n_RCLKD: 1;

TRANSITION pcg.mce.t.b0_pegel_1_1_1
CONSUME pcg.mce.p.RCLKD: 1, pcg.mce.p.pegel_1_1_1: 1;
PRODUCE pcg.mce.p.pegel_1_0_1: 1, pcg.mce.p.n_RCLKD: 1;



{Anbindung an die Auﬂenwelt}

{STOPI produzieren}
TRANSITION pcg.t.STOPI
CONSUME pcg.nor.p.n_STOPI: 1;
PRODUCE pcg.nor.p.STOPI: 1;

{REQI1 produzieren}
TRANSITION pcg.t.REQI1
CONSUME pcg.mutex1.p.n_REQI1: 1;
PRODUCE pcg.mutex1.p.REQI1: 1;

{STRETCH produzieren}
TRANSITION pcg.t.STRETCH
CONSUME pcg.mutex2.p.n_STRETCH: 1;
PRODUCE pcg.mutex2.p.STRETCH: 1;

{ACKI1 abr‰umen}
TRANSITION pcg.t.ACKI1
CONSUME pcg.mutex1.p.ACKI1: 1;
PRODUCE pcg.mutex1.p.n_ACKI1: 1;

{LCLK abr‰umen}
TRANSITION pcg.t.LCLK
CONSUME pcg.mce.p.LCLK: 1;
PRODUCE pcg.mce.p.n_LCLK: 1;



{ vim: set ft=lola: }
